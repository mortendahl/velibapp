apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {

    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "app.mortendahl.velib"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 9
        versionName "0.1.4"
    }

    signingConfigs {
        release {
            storeFile file('../../../mortendahlapp.keystore')
            keyAlias 'google play release'
            storePassword ''    // never record password here! code below for asking at compile time
            keyPassword ''      // never record password here! code below for asking at compile time
        }
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

    }

}

def askAndSetSigningPassword() {

    def password = null

    // first: try command line
    if (password == null) {
        password = System.properties["keystorePassword"]
    }

    // second: try environment
    if (password == null) {
        password = System.getenv("keystorePassword")
    }

    // third: ask user
    if (password == null) {
        if (System.console() != null) {
            // console available, use it to ask for password
            password = new String(System.console().readPassword("\nEnter keystore password: "))
        }
    }

    android.signingConfigs.release.storePassword = password
    android.signingConfigs.release.keyPassword = password

}

//import java.util.regex.Pattern
//
//def increaseVersionCodeInManifest() {
//
//    def manifestFile = file("src/main/AndroidManifest.xml")
//    def pattern = Pattern.compile("android:versionCode=\"(\\d+)\"")
//    def manifestText = manifestFile.getText()
//    def matcher = pattern.matcher(manifestText)
//    matcher.find()
//
//    def oldVersionCode = Integer.parseInt(matcher.group(1))
//    def newVersionCode = oldVersionCode + 1
//
//    println "Setting version code to ${newVersionCode}"
//    def manifestContent = matcher.replaceAll("android:versionCode=\"" + newVersionCode + "\"")
//    manifestFile.write(manifestContent)
//
//    android.defaultConfig.versionCode = newVersionCode
//
//}
//
//def updateVersionNameInManifest() {
//
//    def manifestFile = file("src/main/AndroidManifest.xml")
//    def pattern = Pattern.compile("android:versionName=\"(.+)\"")
//    def manifestText = manifestFile.getText()
//    def matcher = pattern.matcher(manifestText)
//    matcher.find()
//
//    def d = new Date(System.currentTimeMillis())
//    def year = d.getYear() + 1900
//    def month = d.getMonth() + 1
//    def date = d.getDate()
//    def newVersionName = String.format("0.%d.%d", (year-2014) * 12 + month, date)
//
//    println "Setting version name to ${newVersionName}"
//    def manifestContent = matcher.replaceAll("android:versionName=\"" + newVersionName + "\"")
//    manifestFile.write(manifestContent)
//
//    android.defaultConfig.versionName = newVersionName
//
//}

gradle.taskGraph.whenReady { taskGraph ->

    if (taskGraph.hasTask(':mobile:assembleRelease')) {
        askAndSetSigningPassword()
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    wearApp project(':wear')
    compile 'com.android.support:appcompat-v7:22.1.1'

    compile 'com.google.android.gms:play-services-maps:7.5.0'
    compile 'com.google.android.gms:play-services-location:7.5.0'
    compile 'com.google.maps.android:android-maps-utils:0.3+'

    compile 'de.greenrobot:eventbus:2.4.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.3.2@aar') {
        transitive = true;
    }
}
